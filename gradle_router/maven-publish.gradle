// ------------------------------------------------------------------------
// 使用maven插件中的发布功能
// ------------------------------------------------------------------------

apply plugin: 'maven'

// ------------------------------------------------------------------------
// 读取工程配置
// ------------------------------------------------------------------------

Properties rootProjectProperties = new Properties()
rootProjectProperties.load(project.rootProject.file('gradle.properties').newDataInputStream())
def POM_URL = rootProjectProperties.getProperty("POM_URL")
def GROUP_ID = rootProjectProperties.getProperty("GROUP_ID")
def VERSION_NAME = rootProjectProperties.getProperty("VERSION_NAME")

Properties childProjectProperties = new Properties()
childProjectProperties.load(project.file('gradle.properties').newDataInputStream())
def POM_ARTIFACT_ID = childProjectProperties.getProperty("POM_ARTIFACT_ID")

println("maven-publish POM_URL = $POM_URL")
println("maven-publish GROUP_ID = $GROUP_ID")
println("maven-publish VERSION_NAME = $VERSION_NAME")
println("maven-publish POM_ARTIFACT_ID = $POM_ARTIFACT_ID")

// ------------------------------------------------------------------------
// 发布到本地 maven 仓库的任务
// ------------------------------------------------------------------------

uploadArchives {
    repositories {
        mavenDeployer {

            // 填入发布信息
            repository(url: uri(POM_URL)) {
                pom.groupId = GROUP_ID
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION_NAME
            }

            // 修改 router-processor 的 build.gradle 内容
            // 原本内容：dependencies { implementation project(':router-annotations') }
            // 修改后的内容：dependencies { implementation 'com.watayouxiang.router:router-annotations:1.0.0' }
            pom.whenConfigured { pom ->
                pom.dependencies.forEach { dep ->
                    if (dep.getVersion() == "unspecified") {
                        dep.setGroupId(GROUP_ID)
                        dep.setVersion(VERSION_NAME)
                    }
                }
            }
        }
    }
}